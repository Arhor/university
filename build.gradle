plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'idea'
    id 'jacoco'
}

apply from: './gradle/database.gradle'

group = 'by.bsu.uir'
version = '0.0.1-SNAPSHOT'
description = 'university'

sourceCompatibility = '13'
targetCompatibility = '13'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs += [
//            '--enable-preview',
            '-Xlint:preview',
            '-Xlint:unchecked',
    ]
}

bootRun {
    args = ['--spring.profiles.active=dev']
}

task webappCopy(type: Copy, dependsOn: ':webapp:build') {
    from 'webapp/build'
    into 'build/resources/main/static/.'
}

compileJava.dependsOn webappCopy

jacocoTestReport {
    afterEvaluate {
        getClassDirectories() from classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/domain/**",
                    "**/dto/**",
                    "**/error/**",
                    "**/config/**",
            ])
        }
    }
}

test.finalizedBy jacocoTestReport

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'com.h2database:h2'
    implementation 'org.modelmapper:modelmapper:2.3.5'

    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}