plugins {
    id 'org.springframework.boot'        version '2.2.5.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs.addAll([
                '--enable-preview',
                '-Xlint:preview',
                '-Xlint:unchecked',
                '-Xlint:deprecation'
        ])
    }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.forkOptions.with {
            memoryMaximumSize = '1g'
            jvmArgs = ['-XX:MaxPermSize=512m']
        }
    }
}

configure([
        project (':university-core'),
        project (':university-server'),
]) {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    configurations {
        testImplementation {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    test {
        jvmArgs(['--enable-preview'])
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        afterEvaluate {
            getClassDirectories() from classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/domain/**',
                        '**/dto/**',
                        '**/error/**',
                        '**/config/**',
                ])
            }
        }
    }
}