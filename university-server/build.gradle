plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'by.arhor'
version = '0.0.1-SNAPSHOT'

description = 'university-server'

sourceCompatibility = '11'
targetCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs += [
            '-Xlint:preview',
            '-Xlint:unchecked',
    ]
}

bootRun {
    args = ['--spring.profiles.active=dev']
}

task clientCopy(type: Copy, dependsOn: ':university-client:build') {
    from '../university-client/build'
    into 'build/resources/main/static/.'
}

bootJar.dependsOn clientCopy

jacocoTestReport {
    afterEvaluate {
        getClassDirectories() from classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/domain/**',
                    '**/dto/**',
                    '**/error/**',
                    '**/config/**',
            ])
        }
    }
}

test.finalizedBy jacocoTestReport

repositories {
    mavenCentral()
}

dependencies {
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'

    implementation project(':core')

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'com.h2database:h2'
    implementation 'org.modelmapper:modelmapper:2.3.5'

    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}
