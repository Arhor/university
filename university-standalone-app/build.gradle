plugins {
    id 'org.springframework.boot'        version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.node-gradle.node'     version '2.2.3'
    id 'io.freefair.lombok'              version '5.0.0-rc6'
    id 'java'
}

group = 'by.arhor'
version = '0.0.1-SNAPSHOT'

description = 'university-standalone-app'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

node {
    version  = '13.11.0'
    download = true
}

bootRun {
    args = ['--spring.profiles.active=dev']
    jvmArgs(javaModules.collect { "--add-opens=${it}=ALL-UNNAMED" })
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc'

    implementation project(':university-core')

    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.modelmapper:modelmapper:2.3.6'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'com.h2database:h2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

def jsRootDir = "${project.projectDir}"

task npmAuditFix (type: NpmTask, dependsOn: npmInstall) {
    description = 'Verify and fix vulnerabilities in dependencies'
    workingDir = file(jsRootDir)
    args = ['audit', 'fix']
}

task clientBuild (type: NpmTask, dependsOn: npmAuditFix) {
    description = 'Builds production version of the university standalone app client'
    workingDir = file(jsRootDir)
    args = ['run', 'build']
}

task clientCopy(type: Copy, dependsOn: clientBuild) {
    from 'dist/'
    into 'build/resources/main/static/'
}

compileJava { dependsOn(clientCopy) }
